= State Charts for Clojure(script)
:author: Tony Kay
:lang: en
:encoding: UTF-8
:doctype: book
:source-highlighter: coderay
:source-language: clojure
:toc: left
:toclevels: 3
:sectlinks:
:sectanchors:
:leveloffset: 1
:sectnums:
:imagesdir: images
:scriptsdir: js
:imagesoutdir: generated/images

ifdef::env-github[]
:tip-caption: :bulb:
:note-caption: :information_source:
:important-caption: :heavy_exclamation_mark:
:caution-caption: :fire:
:warning-caption: :warning:
endif::[]

ifdef::env-github[]
toc::[]
endif::[]

= Getting Started

A statechart is defined as a (nested) map. Some of this content's behavior is <<Goals, configurable>>.
The simplest setup is to use Clojure for the executable code and a simple flat data model that scopes all state machine
to the session (running instance of state chart).

NOTE: The SCXML standard is used for the semantics and processing, and MUCH of the overall structure. We keep
the idea that each node in the chart has a unique ID, but take some license with executable content.
Most of the "executable content" elements described in the SCXML standard have differences since we are not
using XML. Sometimes we assume you can get by with an expression on the parent element, or just a single child
script node.

To make it easier to write the maps there are functions for each element type in the
`com.fulcrologic.statecharts.elements` namespace, and it is recommended that you use these because some of
them validate their parameters and children.

Once you have a machine definition you need to create a session, which is just a running instance of the
machine.

Once you have a session, you can send events to it, and look at the content of the session (or store it, etc).

Here's a traffic light example from the `src/examples` directory in this repository that
leverages parallel and compound states to simulate traffic lights with pedestrian signals:

[source]
-----
include::src/examples/traffic_light.cljc[]
-----

If you run the items in the comment block, you'll see:

[source]
-----
(:cross-ew/red :cross-ns/white :east-west/green :north-south/red)
(:cross-ew/red :cross-ns/flashing-white :east-west/green :north-south/red)
(:cross-ew/red :cross-ns/flashing-white :east-west/yellow :north-south/red)
(:cross-ew/white :cross-ns/red :east-west/red :north-south/green)
(:cross-ew/flashing-white :cross-ns/red :east-west/red :north-south/green)
(:cross-ew/flashing-white :cross-ns/red :east-west/red :north-south/yellow)
(:cross-ew/red :cross-ns/white :east-west/green :north-south/red)
-----

History support includes both shallow and deep. Here's a shallow example:

[source]
-----
include::src/examples/history_sample.cljc[]
-----

See the https://www.w3.org/TR/2015/REC-scxml-20150901/[SCXML spec] for how to structure elements. The structure and naming are kept close to that spec for easy
cross-referencing with its documentation.

= Going Deeper

You can use the http://www.w3.org/TR/2015/REC-scxml-20150901/[SCXML guide] which has examples as a pretty
good reference for documentation. The document structure in XML is nearly identical to the data structure
of this library:

[source, xml]
-----
<scxml>
  <state id="A"/>
  ...
  </scxml>
-----

[source]
-----
(machine {} ; `machine` is an alias for scxml, which also exists in this library
  (state {:id :A})
  ...)
-----

See the docstrings on the `machine` and various elements for details of differences.

== Terminology

Atomic State:: A state that does not have substates.

Compound State:: A state that has child *states*, where at most *one* child is active at any given time.

Parallel State:: A state that has more than one compound substate, all of which is active at the same time, and
thus have a child state that is active.

Configuration:: When used in the context of a state chart, the configuration is the list of all active states. A
state is active if it or ANY of its children are active. Thus, a parallel machine with hierarchical states may
have a rather large set of IDs in its current configuration.

Working Memory:: A map of data that contains the current configuration of the state machine, and other information
 necessary to know its current full state (which states have had their data model initialized, possible data model
 tracking, etc.).

DataModel:: An implementation of the data model protocol defines how the data model of your chart works. The simple
implementation places the data in Working Memory, and treats lookups as paths into a single top-level map scoped
to the session (running chart instance).

ExecutionModel:: An implementation of this protocol is handed the expressions that are included on the state chart, and
chooses how to run them. A default implementation requires that they be Clojure `(fn [env data])`, and simply calls them
with the contextual data for their location.

External Event:: An event that came from the external API, either by you calling `process-event` or some other
actor code doing so (invocations, sends from external machines, etc.).

Internal Event:: An event that came from the session that will process it. These come from the
machine implementation itself (e.g. done evens) and from you using the `raise` element in an
executable content position.

EventQueue:: A FIFO queue for external event delivery. The event queue is responsible for holding onto pending
External Events to "self", possibly other statechart sessions (instances of other machines),
remote services (e.g. send an email), and for delayed delivery. The default implementation is a "manually polled" queue
that does *not* support most of
these features, and the delayed event delivery is manual (e.g. you have to poll it, or ask it when the next one should
happen and start a timer/thread). Creating a system that runs the loop and does the timed delivery is typically
how you would use these in a more serious/production environment.

Processor:: An implementation of the statechart algorithm. This library comes with an implementation that follows
(as closely as possible) the SCXML recommended standard from 2015. You may provide your own.

Session:: The combination of the content of the DataModel and Working Memory. I.e. all of the data you'd need in order
to resume working from where you last left off. Sessions typically have a unique ID, which could be used to store
sessions into durable storage when they are "idle", and are used for cross-session events.

Conditional Element::
In statecharts there is a node type (represented as a diamond) that represents a state in which the machine
never "rests", but instead immediately transitions to another node based on predicate expressions. In this
library (and SCXML) this is modelled with a state that has more than one transition, NONE of which have
and `:event` qualifier (meaning they are exited as soon as they are entered, assuming there is a valid transition).

== States

States may be atomic, compound (hierarchical), or parallel. The first two are generated with the `state` element, and
the latter with `parallel`.

=== Compound States

=== Parallel States

== Transitions

Transitions are the consumers of events. Their "source" is their parent. Transitions can be *eventless*, and
they can also have no *target* (indicating they do not change the active states, and are only for executing
their content).

A state can have any number of transition elements. These are tested *in order* of appearance, and the
first transition that matches the current circumstance is taken. Transitions are *enabled* when:

* Their `cond` (if present) evaluates to true AND
* Their `event` (if present) matches the current event's name (See event name matching)
* OR neither are present.

== "Condition" States

Condition states from classic state charts
(shown as a diamond in UML state chart notation) can be modelled using eventless transitions.

An "eventless" transition without a `:cond` is always enabled.

Below is a conditional state that when entered will immediately transition to either state :X or :Y, depending
on the result of the first transition's condition expression:

[source]
-----
(state {:id :Z}
  (transition {:cond positive-balance? :target :X})
  (transition {:target :Y}))
-----

== Event Processing

In a fully-fleshed system your event queue would have a corresponding runtime story, where `process-event` was
automatically called on the correct machine/session/invocation/service when an event is available. As such,
and event queue might be distributed and durable (e.g. using Kafka or the like), or might simply be something
that runs in-core (a core.async `go-loop`).

=== Event Name Matching

The SCXML standard describes a name matching algorithm for event names that allows for wildcard pattern matching.
Event names are dot-separated, and are prefix-matched. In this library keywords are used for event names, but
other than that the interpretation is as specified in the standard. For example,
the event name `:a.b.c` is matched by `:a.b.c.\*`, `:a.b.c`, `:a.b.*`, `:a.b`, etc. That is to say that
a transition with `:event :a.b` would be enabled by event `:a.b.c`.

The wildcard is always implied, but can be specified on a transition for clarity. Transitions are enabled and
matched "in order", so you can model narrowed and catch-all behavior:

[source]
-----
(state {:id A}
  (transition {:event :error.communication} ...)
  (transition {:event :error} ...))
-----

where the first transition will be enabled only if the error is a communication one, and all errors will enable
the second.

See the SCXML standard for the rules on transition preemption regarding conflicting transitions that
match at the same time.

=== Send Targets

The SCXML standard (section 6.2.4) allows the format of the target to change based on the type, and implies that
these are often URLs. The default implementation of the event queue in this library only supports
targeting other statecharts (see Send Types below), and when using the default send type, the target is
expected to simply be a `session-id`, where `session-id`  is a unique identifier, such as a GUUID.

Other implementations of the EventQueue protocol may choose to define further refinements.

=== Send Types

The SCXML standard (section 6.2.5) defines types as a URI as well. The internal implementations in this library
do not enforce this, but this library *does* recognize the string "http://www.w3.org/TR/scxml/#SCXMLEventProcessor"
as a desire to talk to another statechart session (and is the only/default predefined value).

=== Manual Polling Queue

There is a manually polled queue implementation in this library, with *no* automatic processing
at all. If you want to support events that come from outside of the machine via the queue, then you have
to create a loop that polls the queue and calls `process-event!`.  If you do this, then event the
delayed event delivery will work, as long as your code watches for the delayed events to appear on the queue.

[source]
------
include::src/examples/traffic_light.cljc[]
------

=== Autonomous State Charts

You can, of course, write your own "event loop". The library comes with a core.async event queue that works
in conjunction with the simple model to provide an event loop that will automatically process events as they come
in, and will allow the machine to send itself external events (usually done in order to get a delay). Note that the
`raise` element does not use the external queue, but also does not provide delays.

Below is an example of using the core.async queue to run a traffic light that
automatically changes over time:

[source]
------
include::src/examples/traffic_light_async.cljc[]
------

NOTE: The `send` element has a `Send` (capital S) alias so you can avoid shadowing the Clojure function.

== Data Models

A data model is a component of the system that holds data for a given state chart's session.
The SCXML specification allows the implementation quite a bit of latitude in the interpretation of the chart's
data model. You could define scopes that nest, one global map of data that is visible everywhere, or hook your
data model to an external database.

See the docstrings in the `protocols` namespace.

There is a predefined `FlatWorkingMemoryDataModel` (in `data-model.working-memory-data-model`) that puts all data into
a single scope (a map which itself can be a nested data structure). This is the recommended model for ease of use.

There is also a predefined `WorkingMemoryDataModel` in that scopes data to the state in which it is declared.

Both of these put the real data in the session working memory, allowing the data, for example, to be persisted with the state of
a running session when pausing it for later revival.

== Execution Models

Most people will probably just use the CLJCExecutionModel, which lets you write executable content
in CLJC as lambdas:

[source]
-----
;; Use `data` to compute when this transition is "enabled"
(transition {:event :a :cond (fn [env data] (your-predicate data))}
  ;; Run some arbitrary code on this transition
  (script {:expr (fn [env data] ...)}))
-----

There is a macro version of the `script` element called `sfn` that can be used as a shorthand for
script elements:

[source]
-----
;; Use `data` to compute when this transition is "enabled"
(transition {:event :a :cond (fn [env data] (your-predicate data))}
  ;; Run some arbitrary code on this transition
  (sfn [env data] ...)))
-----


== Working Memory and Identity

The working memory of the state machine is plain EDN and contains no code.
It is serializable by nippy, transit, etc. Therefore, you can easily save
an active state machine by value into any data store. The value
is intended to be as small as possible so that storage can be efficient.

Every active state machine is assigned a ID on creation (which you
can override via `initialize`). This is intended as part of the story to
allow you to coordinate external event sources with working with
instances of machines that are archived in durable storage while idle.

== Invocations

An invocation is an "external" service/chart/process that can be started when your state machine enters a state,
exchanges events *while* that state is active (you can forward events and receive them). The invocation can self-terminate
while the state is still active, but it will also be cancelled if your chart leaves the enclosing state.

For example, suppose you've installed a service that can provide events on some interval. That service send back
`:timer` events that include `sequence-number` in the event data. Suppose you want to use that to trigger reminder
emails every two days while the billing system is in a Grace Period state for that user
(one statechart session per user).

You might end up with something like:

[source]
-----
(state {:id :BillingGracePeriod}
  (transition {:event :timer}
    (send {:type :email
           :event :billing-reminder
           :namelist {:to [:ROOT :user/email]
                      :reminder [:ROOT :reminders]}}))
  (invoke {:type :interval-timer
           :finalize (fn [env {:keys [sequence-number]}] [(ops/assign [:ROOT :reminders] sequence-number)])
           :params {:period "2 days"}}))
-----

The `transition` element responds to the events received by the state chart, and `invoke` can send such events. So,
the timer service here is sending `:timer` events, and then the `send` element is pulling data from the DataModel
to construct the correct parameters to send to an email service that uses the event type (:billing-reminder) and
the parameter data (generated in this case with `namelist`) to fill out some kind of email template.

NOTE: The implementation of the invocation subsystem will define how these services are written and installed. It is
still under construction at the time of this writing. Consult the source in case the docs are out of date).

Exiting the `:BillingGracePeriod` state will cancel the timer so that no further `:timer` events are sent.

= Custom Executable Content

The SCXML standard defines a number of elements it terms "Executable Content", meaning child nodes that
do some action. For example, `send` is executable content of this `on-entry` node:

[source]
-----
(state {}
  (on-entry {}
     (send ...)))
-----

The standard allows for a compliant implementation to include extra executable content, but
this library takes that one step further and allows *you* to define new executable content elements.

Each node in the data graph of the statecharts in this library (which are just nested maps)
has a `:node-type` entry (in the case of send, its value is `:send`). The implementation of v20150901
allows you to extend the set of node types that "do things" as executable content. All you need to do
is create a map that has a new (preferably namespaced) node type, and then create a defmethod that
handles that type:

[source]
-----
(ns my-node-types
  (:require
    [com.fulcrologic.statecharts.algorithms.v20150901-impl :as impl]))

(defn my-exec [opts]
  (merge opts {:node-type ::my-exec}))

(defmethod impl/execute-element-content! ::my-exec [env element] ...)
-----

and you can use your `(my-exec {...})` anywhere executable content is allowed in the state chart. The
helper function `impl/execute!` should be used to run any child nodes (if your node allows other
executable children).

[source]
-----
(ns my-node-types
  (:require
    [com.fulcrologic.statecharts.algorithms.v20150901-impl :as impl]))

(defn log-and-run [& children]
  (merge opts {:node-type ::log-and-run
               :children (vec children)}))

(defmethod impl/execute-element-content! ::log-and-run [env element]
  (log/debug "Running: " element)
  ;; The `execute!` method will run executable children of an element (which will call execute-element-content! on
  ;; each of them)
  (impl/execute! env element))

;; ...

(machine {}
  (state {}
    (log-and-run
       (send {})
       (log {})
       (raise {}))))
-----

= Testing

The main interface to this library is pure and functional, which makes the general job of testing easier in some cases,
but the fact is that you often need to walk a state machine through several steps in order to get it *into* the
starting configuration to test.

The fact that nodes can execute (possibly side-effecting) code also means that when testing it is usually desirable to
eliminate these side effects through some kind of stubbing mechanism.

Fortunately the design of the library makes it trivial to plug in a mock execution model, mock event queue,
and a test data model, which allows you to easily exercise a state machine in tests in ways that do not
side effect in an uncontrolled fashion.

The `com.fulcrologic.statecharts.testing` namespace includes mocks for all of the protocols, allows for
a pluggable data model (defaults to flat working memory), and has pre-written helpers and predicates.

The mock execution model allows you to easily set up specific results for expressions and conditions that
would normally be run.

Here is an example test (written with fulcro-spec):

[source]
-----
(ns statecharts-spec
  (:require
    [app.my-machine :refer [some-statechart]]
    [com.fulcrologic.statecharts.algorithms.v20150901 :refer [new-processor]]
    [com.fulcrologic.statecharts :as sc]
    [com.fulcrologic.statecharts.state-machine :refer [machine]]
    [com.fulcrologic.statecharts.events :refer [new-event]]
    [com.fulcrologic.statecharts.elements :refer [state on-entry]
    [com.fulcrologic.statecharts.execution-model.lambda :refer [new-execution-model]]
    [com.fulcrologic.statecharts.protocols :as sp]
    [com.fulcrologic.statecharts.testing :as testing :refer [in? ran? will-send configuration-for-states]]
    [fulcro-spec.core :refer [specification assertions component provided when-mocking! =>]]
    [clojure.set :as set]))

(defn is-tuesday? [env data] ...)
(def some-statechart
  (machine {}
    (state {:id :state/start}
      (transition {:cond is-tuesday?
                   :event :event/trigger
                   :target :state/next}))
    (state {:id :state/next})))

(defn config [] {:machine some-statechart})

(specification "My Machine"
  (component "When it is tuesday"
    (let [env (testing/new-testing-env (config) {is-tuesday? true})]

      (testing/start! env session-id)
      (testing/run-events! env :event/trigger)

      (assertions
        "Checks to see that it is tuesday"
        (testing/ran? env is-tuesday?) => true
        "Goes to the next state"
        (testing/in? env :state/next) => true))))
-----

== Mocking

The mocking (set when creating the testing env) is a simple map (e.g. `{is-tuesday? true}`). The keys of the map
must exactly match the expression in question (e.g. use `defn` to make them (as shown above), and then use those as the
cond/expr so you can match on it). The values in the mocking map can be literal values OR functions. If they are functions
then they will be passed the `env`, which will include the special key `:ncalls` that will have the count (inclusive) of
the number of times that expression has run since the test env was created.

== Event Sends/Cancels

The sends and cancels will also be auto-recorded. See the docstrings in the testing namespace for more information.

== Starting in a Specific Configuration

Most of your tests will need the machine to be in some particular state as part of your setup. You *could* get there
by triggering a sequence of events while having all of the mocks in perfect condition, but this creates a fragile
test where changes to the structure of the chart break a bunch of tests. The testing helpers include
`(testing/goto-configuration! env data-ops config)` that allows you to set up the data model and configuration.

The `data-ops` is a vector of Data Model operations (e.g. ops/assign) to run on the data model. and `config` is
a valid configuration (set of active states) for the chart.
Unfortunately, the configuration of a statechart is non-trivial (it must include all active states in a complex
hierarchy) and will change when you refactor the machine. So, another helper `testing/configuration-for-states` allows you
to get a list of *all* of the states that would be active *given* the deepest desired leaf state(s).

In a chart with no parallel states, there will only ever be one leaf, but when parallel states are active you must
list a valid leaf from each region.

Thus, a common test setup will look like this:

[source]
-----
(defn config [] {:machine some-statechart})

(specification "Starting in Some State"
  (let [env (testing/new-testing-env (config) {})]

    ;; assume a top-level parallel node, with two sub-regions. `configuration-for-states` will populate all of the necessary
    ;; ancestor states
    (testing/goto-configuration! env [] (configuration-for-states env #{:state.region1/leaf :state.region2/leaf}))
    (testing/run-events! env :event/expired)

    ;; assertions
    ))
-----

= Relationship to SCXML

This library's internal implementation follows (as closely as possible) the official
https://www.w3.org/TR/2015/REC-scxml-20150901/#AlgorithmforSCXMLInterpretation[State Chart XML Algorithm]. In fact,
much of the implementation uses internal volatiles in order to match the imperative style of that doc for easier
comparison and avoidance of bugs.

The actual structure of the live CLJC data used to represent machines also closely mimics
the structure described there, but with some differences for convenient use in CLJC.

Specifically, executable content is *still* treated as *data*, but the XML nodes that
are described in the standard do *not* all exist in this library, because a conformant
XML reader (which would need to be aware of the target execution model) can easily
translate such nodes into the target data representation (even if that target
representation is script strings).

Some of the data model elements are also abbreviated in a similar manner. See
the docstrings for details.

Thus, if you are trying to read SCXML documents you will need to write (or find) an
XML reader that can do this interpretation.

For example, an XML reader that targets https://github.com/babashka/sci[sci] (the
Clojure interpreter) might convert the XML (where `a` and `do-something` are implied
values in the data and excution model):

[source, xml]
-----
<if cond="(= 1 a)">
  (let [b (inc a)]
    (do-something b))
</if>
-----

into (scope and args still determined by the execution model selected):

[source, clojure]
-----
;; String-based interpretation
(script {:expr
  "(if (= 1 a)
     (let [b (inc a)]
       (do-something b)))"})

;; OR eval-based
(script {:expr
  '(if (= 1 a)
     (let [b (inc a)]
       (do-something b)))})

;; OR functional
(script {:expr (fn [env {:keys [a]}]
                  (if (= 1 a)
                    (let [b (inc a)]
                      (do-something b))))})
-----

If you're using XML tools to generate you machines, though, it's probably easiest to use
`script` tags to begin with.

== Related Work

The primary alternative to this library is https://github.com/lucywang000/clj-statecharts[clj-statecharts],
which is a fine library modelled after xstate.

This library exists for the following reasons:

* At the time this library was created, https://github.com/lucywang000/clj-statecharts/[clj-statecharts] was missing features. In particular history nodes,
  which we needed. I looked at clj-statecharts in order to try to add history, but some of the internal
  decisions made it more difficult to add (with correct semantics) and the Eclipse license made it less
  appealing for internal customization as a base in commercial software (see https://www.juxt.pro/blog/prefer-mit).
* To create an SCXML-like implementation that uses the algorithm defined
  in the W3C Recommended document, and can (grow to) run (with minor transformations) SCXML docs that are
  targeted to Clojure with the semantics defined there (such as they are).
* To define more refined abstract mechanisms such that the state charts can be associated to long-lived things
  (such as a monetary transaction that happens over time) and
  be customized to interface with things like durable queues for events (e.g. AWS SQS) and
  reliable timers.
* MIT licensing instead of Eclipse.

Other related libraries and implementations:

* https://xstate.js.org/[XState] : Javascript. Could be used from CLJS.
* https://commons.apache.org/proper/commons-scxml/[Apache SCXML] : Stateful and imperative. Requires writing classes. Requires you use XML.
* https://github.com/fulcrologic/fulcro/blob/develop/src/main/com/fulcrologic/fulcro/ui_state_machines.cljc[Fulcro UI State Machines]
: A finite state machine namespace (part of Fulcro) that is tightly coupled to Fulcro's needs (full stack operation in the context of
Fulcro UI and I/O).

== Conformance

This library was written using the reference implementation described in
the https://www.w3.org/TR/scxml[SCXML standard], but without the requirement
that the machine be written in XML.

Any deviation from the standard (as far as general operation of state transitions, order
of execution of entry/exit, etc.) should be considered a bug. Note that it is possible
for a bugfix in this library to change the behavior of your code (if you wrote it in
a way that depends on the misbehavior); therefore, even though
this library does not intend to make breaking changes, it is possible that a bugfix could affect
your code's operation.

If future versions of the standard are released that cause incompatible changes, then
this library will add a new namespace for that new standard (not break versioning).
